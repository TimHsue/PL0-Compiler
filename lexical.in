 <程序> → <分程序>.
 <分程序> → [<常量说明部分>][<变量说明部分>][<过程说明部分>] <语句>
 <常量说明部分> → const <常量定义>{,<常量定义>};
 <常量定义> → <标识符>=<无符号整数>
 <无符号整数> → symbol (const)
 <变量说明部分> → var <标识符>{,<标识符>};
 <标识符> → symbol (identifier)
 <过程说明部分> → <过程首部><分程序>;{<过程说明部分>}
 <过程首部> → procedure<标识符>;
 <语句> → <赋值语句>|<条件语句>|<当型循环语句>|<过程调用语句>|<读语句>|<写语句>|<复合语句>|<空>
 <赋值语句> → <标识符>:=<表达式>
 <复合语句> → begin<语句>{;<语句>}<end>
 <条件> → <表达式><关系运算符><表达式>|odd<表达式>
 <表达式> → [+|-]<项>{<加减运算符><项>}
 <项> → <因子>{<乘除运算符><因子>}
 <因子> → <标识符>|<无符号整数>|(<表达式>)
 <加减运符> → +|-
 <乘除运算符> → *|/
 <关系运算符> → =|#|<|<=|>|>=
 <条件语句> → if<条件>then<语句>
 <过程调用语句> → call<标识符>
 <当型循环语句> → while<条件>do<语句>
 <读语句> → read(<标识符>{,<标识符>})
 <写语句> → write(<标识符>{,<标识符>})
